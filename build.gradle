plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version "1.1.16"
}



version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "Parchment"
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = "mixin-extras"
		url = "https://jitpack.io"
		content {
			includeGroupByRegex "com.github.*"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.06.26@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Mixin^2 for the more *cursed* mod compat
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixin_squared_version}")))
}

loom {
	accessWidenerPath = file("src/main/resources/fakerlib.aw")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {

	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "fakerlib"
	versionType = "beta"
	versionName = "Fakerlib " + project.version
	uploadFile = remapJar
	gameVersions = ["1.20.1"]
	loaders = ["fabric"]
	changelog = "[Changelog](https://github.com/TheWinABagel/FakerLib/blob/master/changelog.md)"
	//debugMode = true
	dependencies {
		required.project "fabric-api"
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("cf_token")
	//debugMode = true
	def mainFile = upload(853197, remapJar)
	mainFile.changelog = "https://github.com/TheWinABagel/FakerLib/blob/master/changelog.md"
	mainFile.releaseType = "beta"
	//mainFile.addJavaVersion("17") //doesn't work for reasons unknown to me
	mainFile.addGameVersion("1.20.1")
	mainFile.addModLoader("fabric")
	mainFile.displayName = project.archives_base_name + "-" + project.version
	mainFile.addRequirement("fabric-api") //FAPI
}
